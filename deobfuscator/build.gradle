import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id "com.github.hauner.jarTest" version "1.0.1"
}

description = 'Deobfuscator'

configurations {
    deobjars
}

dependencies {
    implementation project(':runelite-api')
    implementation project(':runescape-api')
    implementation group: 'net.runelite', name: 'fernflower', version: '20171017'
    implementation group: 'com.google.guava', name: 'guava', version: '28.0-jre'
    implementation group: 'org.slf4j', name: 'slf4j-api', version: '1.7.26'
    implementation group: 'com.google.code.gson', name: 'gson', version: '2.8.5'
    implementation group: 'org.ow2.asm', name: 'asm-debug-all', version: '5.2'
    runtime group: 'org.slf4j', name: 'slf4j-simple', version: '1.7.26'
    deobjars project(':rs-client')
    deobjars group: 'net.runelite.rs', name: 'vanilla', version: '181'
    testImplementation configurations.deobjars.dependencies
    testImplementation group: 'junit', name: 'junit', version: '4.12'
    testImplementation group: 'org.mockito', name: 'mockito-core', version: '3.0.0'
}

processResources {
    from file("src/main/resources/deob.properties"), {
        filter(ReplaceTokens, tokens: [
                "rs.version": rsversion.toString(),
                "vanilla.jar": configurations.deobjars.find {it.name.startsWith("vanilla")}.toString().replace('\\', "/"),
                "rs.client": configurations.deobjars.find {it.name.startsWith("rs-client")}.toString().replace('\\', "/")
        ])
    }
}
processTestResources {
    from file("src/test/resources/deob-test.properties"), {
        filter(ReplaceTokens, tokens: [
                "rs.client": configurations.deobjars.find {it.name.startsWith("rs-client")}.toString().replace('\\', "/"),
                "rs.version": rsversion.toString(),
                "vanilla.jar": configurations.deobjars.find {it.name.startsWith("vanilla")}.toString().replace('\\', "/")
        ])
    }
}